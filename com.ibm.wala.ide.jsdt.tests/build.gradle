plugins {
	id 'com.diffplug.eclipse.mavencentral'
	id 'wala-java'
}

repositories {
	maven {
		url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
		content {
			includeGroup 'org.eclipse.wst.jsdt'
		}
	}
}

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.runtime',
				'org.eclipse.equinox.common',
				'org.eclipse.osgi',
		].each { dep 'testImplementation', it }
		useNativesForRunningPlatform()
		constrainTransitivesToThisRelease()
	}
}

dependencies {
	testImplementation(
			'junit:junit:4.13.2',
			"org.eclipse.wst.jsdt:core:$eclipseWstJsdtVersion",
			'org.eclipse.platform:org.eclipse.osgi:3.15.100',
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.js'),
			project(':com.ibm.wala.cast.js.rhino'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.ide.jsdt'),
			project(':com.ibm.wala.util'),
			project(configuration: 'testArchives', path: ':com.ibm.wala.ide.tests'),
	)
	testImplementation('javax.annotation:javax.annotation-api') {
		version { strictly '1.3.2' }
	}
}

tasks.named('test') {
	// https://github.com/liblit/WALA/issues/5
	exclude '**/JSProjectScopeTest.class'
}
