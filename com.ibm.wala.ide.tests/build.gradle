plugins {
	id 'com.diffplug.eclipse.mavencentral'
	id 'com.github.hauner.jarTest' version '1.0.1'
	id 'eclipse'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.commands',
				'org.eclipse.core.jobs',
				'org.eclipse.core.resources',
				'org.eclipse.core.runtime',
				'org.eclipse.equinox.common',
				'org.eclipse.jface',
				'org.eclipse.osgi',
				'org.eclipse.ui.ide',
		].each { dep 'testImplementation', it }
		useNativesForRunningPlatform()
	}
}

dependencies {
	testImplementation(
			'org.osgi:org.osgi.core:6.0.0',
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.ide'),
			project(':com.ibm.wala.util'),
	)
	testImplementation('org.eclipse.platform:org.eclipse.ui.workbench') {
		version { strictly '3.120.0' }
	}
	testImplementation('javax.annotation:javax.annotation-api') {
		version { strictly '1.3.2' }
	}
	testImplementation('org.eclipse.platform:org.eclipse.core.resources') {
		version { strictly '3.14.0' }
	}
	testImplementation('org.eclipse.platform:org.eclipse.equinox.common') {
		version { strictly '3.14.100' }
	}
}
