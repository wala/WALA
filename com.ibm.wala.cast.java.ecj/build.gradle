plugins {
	id 'application'
	id 'com.diffplug.gradle.eclipse.mavencentral'
}

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.runtime',
				'org.eclipse.jdt.core',
		].each it.&implementation
	}
}

dependencies {
	implementation(
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.java'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
	)
}

mainClassName = 'com.ibm.wala.cast.java.ecj.util.SourceDirCallGraph'

run {
	// this is for testing purposes
	final def javaTestData = ':com.ibm.wala.cast.java.test.data'
	evaluationDependsOn javaTestData
	args = ['-sourceDir', project(javaTestData).sourceSets.test.java.srcDirs.find(), '-mainClass', 'LArray1']

	// log output to file, although we don't validate it
	final def outFile = file("$temporaryDir/stdout-and-stderr.log")
	outputs.file outFile
	doFirst {
		final def fileStream = outFile.newOutputStream()
		standardOutput fileStream
		errorOutput fileStream
	}
}

apply from: rootProject.file("gradle-mvn-push.gradle")
