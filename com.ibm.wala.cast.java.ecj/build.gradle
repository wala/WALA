plugins {
	id 'application'
	id 'com.diffplug.eclipse.mavencentral'
	id 'gradle-mvn-push'
	id 'wala-java'
}

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.runtime',
				'org.eclipse.jdt.core',
		].each it.&implementation
		constrainTransitivesToThisRelease()
	}
}

dependencies {
	implementation(
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.java'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
	)
	testImplementation(
			'junit:junit:4.13.2',
			testFixtures(project(':com.ibm.wala.cast.java')),
	)
	testRuntimeOnly testFixtures(project(':com.ibm.wala.core'))
}

application.mainClass.set('com.ibm.wala.cast.java.ecj.util.SourceDirCallGraph')

final run = tasks.named('run') {
	// this is for testing purposes
	final javaTestData = ':com.ibm.wala.cast.java.test.data'
	evaluationDependsOn javaTestData
	args = ['-sourceDir', project(javaTestData).sourceSets.test.java.srcDirs.find(), '-mainClass', 'LArray1']

	// log output to file, although we don't validate it
	final outFile = project.layout.buildDirectory.file 'SourceDirCallGraph.log'
	outputs.file outFile
	doFirst {
		final fileStream = outFile.get().asFile.newOutputStream()
		standardOutput fileStream
		errorOutput fileStream
	}
}

tasks.named('test') {
	maxHeapSize = '1200M'

	workingDir project(':com.ibm.wala.cast.java.test.data').projectDir

	// ensure the command-line driver for running ECJ works
	dependsOn run
}
