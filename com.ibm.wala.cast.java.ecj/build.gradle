plugins {
	id 'application'
	id 'com.diffplug.gradle.eclipse.mavencentral'
}

sourceSets.main.java.srcDirs = ['src']

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.runtime',
				'org.eclipse.jdt.core',
		].each it.&implementation
	}
}

dependencies {
	implementation(
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.java'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
	)
}

mainClassName = 'com.ibm.wala.cast.java.ecj.util.SourceDirCallGraph'

run {
	// this is for testing purposes
	final def javaTestData = ':com.ibm.wala.cast.java.test.data'
	evaluationDependsOn javaTestData
	args = ['-sourceDir', project(javaTestData).sourceSets.test.java.srcDirs.find(), '-mainClass', 'LArray1']
}

apply from: rootProject.file("gradle-mvn-push.gradle")
