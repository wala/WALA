plugins {
	id 'gradle-mvn-push'
	id 'wala-java'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

dependencies {
	api(project(':com.ibm.wala.core')) {
		because 'public method AstCGNode.addTarget receives an argument of type CGNode'
	}
	implementation(
			'commons-io:commons-io:2.11.0',
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
	)
	testImplementation(
			'junit:junit:4.13.2',
	)
	testRuntimeOnly testFixtures(project(':com.ibm.wala.core'))
}

tasks.named('javadoc', Javadoc) {
	final jsTasks = project(':com.ibm.wala.cast.js').tasks
	classpath += files jsTasks.named('compileJava', JavaCompile)
	final jsCreatePackageList = jsTasks.named('createPackageList', CreatePackageList)
	final packageListDirectory = jsCreatePackageList.map { it.packageListDirectory.get() }
	it.inputs.dir packageListDirectory

	inputs.property 'extdocURL', jsTasks.named('javadoc', Javadoc).map { it.destinationDir.path }
	inputs.property 'packagelistLoc', packageListDirectory.map { it.toString() }

	doFirst {
		options.linksOffline(
				inputs.properties['extdocURL'],
				inputs.properties['packagelistLoc']
		)
	}
}

final getProjectLibraryDirectory(project_name, link_task_name) {
	final library_project = project(project_name)
	final link_task = library_project.tasks.getByName(link_task_name)
	final library_file = getNativeLibraryOutput(link_task)
	return library_file.parent
}

tasks.named('test') {
	final xlator_project_name = 'xlator_test'
	final link_task_name = 'linkDebug'
	inputs.files project(xlator_project_name).tasks.named(link_task_name)

	doFirst {
		systemProperty 'java.library.path', getProjectLibraryDirectory(xlator_project_name, link_task_name)

		if (isWindows) {
			// Windows has nothing akin to RPATH for embedding DLL search paths in other DLLs or
			// executables.  Instead, we need to ensure that any required DLLs are in the standard
			// executable search path at test run time.
			final path_key = environment.keySet().find { it.equalsIgnoreCase('path') }
			assert path_key != null
			environment[path_key] += ";${getProjectLibraryDirectory(':com.ibm.wala.cast:cast', link_task_name)}"
		}
	}
}
