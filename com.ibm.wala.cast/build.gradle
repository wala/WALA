plugins {
	id 'eclipse'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

dependencies {
	api(project(':com.ibm.wala.core')) {
		because 'public method AstCGNode.addTarget receives an argument of type CGNode'
	}
	implementation(
			'commons-io:commons-io:2.6',
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
	)
	testImplementation(
			'junit:junit:4.13',
	)
}

tasks.named('javadoc', Javadoc) {
	final jsTasks = project(':com.ibm.wala.cast.js').tasks
	classpath += files jsTasks.named('compileJava', JavaCompile)
	final jsCreatePackageList = jsTasks.named('createPackageList', CreatePackageList)
	final packageListDirectory = jsCreatePackageList.flatMap { it.packageListDirectory }
	it.inputs.dir packageListDirectory

	doFirst {
		final jsJavadoc = jsTasks.named('javadoc', Javadoc)
		options.linksOffline(
				jsJavadoc.get().destinationDir.path,
				packageListDirectory.get().toString()
		)
	}
}

tasks.named('compileTestJava') {
	options.headerOutputDirectory.set file('build/headers')

	// ECJ does not implement the "-h" flag required when the
	// headerOutputDirectory option is used, so we must switch
	// back to the default Java compiler for this one task.
	toolChain = it.ext.originalToolChain
	options.compilerArgs = []
}

final def getProjectLibraryDirectory(project_name, link_task_name) {
	final def library_project = project(project_name)
	final def link_task = library_project.tasks.getByName(link_task_name)
	final def library_file = getNativeLibraryOutput(link_task)
	return library_file.parent
}

tasks.named('test') {
	final def xlator_project_name = 'xlator_test'
	final def link_task_name = 'linkDebug'
	dependsOn "$xlator_project_name:$link_task_name"

	doFirst {
		systemProperty 'java.library.path', getProjectLibraryDirectory(xlator_project_name, link_task_name)

		if (isWindows) {
			// Windows has nothing akin to RPATH for embedding DLL search paths in other DLLs or
			// executables.  Instead, we need to ensure that any required DLLs are in the standard
			// executable search path at test run time.
			final def path_key = environment.keySet().find { it.equalsIgnoreCase('path') }
			assert path_key != null
			environment[path_key] += ";${getProjectLibraryDirectory(':com.ibm.wala.cast:cast', link_task_name)}"
		}
	}
}

apply from: rootProject.file("gradle-mvn-push.gradle")
