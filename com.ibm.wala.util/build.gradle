plugins {
	id 'gradle-mvn-push'
	id 'wala-java'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

dependencies {
	testImplementation(
			'junit:junit:4.13.2',
			'org.hamcrest:hamcrest:2.2',
	)
	testRuntimeOnly testFixtures(project(':com.ibm.wala.core'))
}

tasks.named('javadoc') {
	classpath += files project(':com.ibm.wala.core').tasks.named('compileJava', JavaCompile)
	final currentJavaVersion = JavaVersion.current()
	final linksPrefix = currentJavaVersion >= JavaVersion.VERSION_11 ? 'en/java/' : ''
	options.links "https://docs.oracle.com/${linksPrefix}javase/$currentJavaVersion.majorVersion/docs/api/"
	options.source = "8" // workaround https://bugs.openjdk.java.net/browse/JDK-8212233.
}
