plugins {
	id 'com.github.hauner.jarTest'
	id 'eclipse'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

sourceSets.test.java.srcDirs = ['harness-src/java']

tasks.named('compileTestJava') {
	options.headerOutputDirectory.set file('build/headers')

	// ECJ does not implement the "-h" flag required when the
	// headerOutputDirectory option is used, so we must switch
	// back to the default Java compiler for this one task.
	toolChain = it.ext.originalToolChain
	options.compilerArgs = []
}

dependencies {
/*
	api(project(configuration: 'testArchives', path: ':com.ibm.wala.core.tests')) {
		because 'public class TestCallGraphShape extends class WalaTestCase'
	}
*/
	testImplementation(
			'junit:junit:4.12',
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.util'),
	)
}

tasks.named('test') {
	final def xlator_project_name = 'xlator_test'
	final def xlator_link_name = 'linkDebug'
	dependsOn "$xlator_project_name:$xlator_link_name"

	doFirst {
		final def xlator_project = project(xlator_project_name)
		final def xlator_link = xlator_project.tasks.getByName(xlator_link_name)
		final def xlator_lib = getNativeLibraryOutput(xlator_link)
		systemProperty 'java.library.path', xlator_lib.parent
	}
}
