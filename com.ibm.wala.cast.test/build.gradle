plugins {
	id 'com.github.hauner.jarTest'
	id 'eclipse'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

sourceSets.test.java.srcDirs = ['harness-src/java']

tasks.named('compileTestJava') {
	options.headerOutputDirectory.set file('build/headers')

	// ECJ does not implement the "-h" flag required when the
	// headerOutputDirectory option is used, so we must switch
	// back to the default Java compiler for this one task.
	toolChain = it.ext.originalToolChain
	options.compilerArgs = []
}

dependencies {
	testImplementation(
			'junit:junit:4.12',
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.util')
	)
}

final def getProjectLibraryDirectory(project_name, link_task_name) {
	final def library_project = project(project_name)
	final def link_task = library_project.tasks.getByName(link_task_name)
	final def library_file = getNativeLibraryOutput(link_task)
	return library_file.parent
}

tasks.named('test') {
	final def xlator_project_name = 'xlator_test'
	final def link_task_name = 'linkDebug'
	dependsOn "$xlator_project_name:$link_task_name"

	doFirst {
		systemProperty 'java.library.path', getProjectLibraryDirectory(xlator_project_name, link_task_name)

		if (System.getProperty('os.name').startsWith('Windows')) {
			// Windows has nothing akin to RPATH for embedding DLL search paths in other DLLs or
			// executables.  Instead, we need to ensure that any required DLLs are in the standard
			// executable search path at test run time.
			final def path_key = environment.keySet().find { it.equalsIgnoreCase('path') }
			assert path_key != null
			environment[path_key] += ";${getProjectLibraryDirectory(':com.ibm.wala.cast:cast', link_task_name)}"
		}
	}
}
