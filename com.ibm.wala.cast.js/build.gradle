plugins {
	id 'com.github.hauner.jarTest'
}

dependencies {
	api(project(':com.ibm.wala.cast')) {
		because 'public class JSCallGraphUtil extends class CAstCallGraphUtil'
	}
	implementation(
			'commons-io:commons-io:2.6',
			'net.htmlparser.jericho:jericho-html:3.2',
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
	)
	testImplementation(
			'junit:junit:4.13',
			testFixtures(project(':com.ibm.wala.testutil')),
	)
}

tasks.register('createPackageList', CreatePackageList) {
	sourceSet sourceSets.main
}

tasks.named('javadoc') {
	def rhinoName = ':com.ibm.wala.cast.js.rhino'
	dependsOn "$rhinoName:compileJava"

	doFirst {
		classpath += files project(rhinoName).compileJava
	}
}

tasks.named('processTestResources') {
	def data = project(':com.ibm.wala.cast.js.test.data')
	dependsOn data.tasks.named('processTestResources')
	from data.sourceSets.test.resources
}

tasks.named('test') {
	maxHeapSize = '800M'
}

apply from: rootProject.file("gradle-mvn-push.gradle")
