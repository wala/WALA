dependencies {
	api(project(':com.ibm.wala.cast')) {
		because 'public class JSCallGraphUtil extends class CAstCallGraphUtil'
	}
	implementation(
			'commons-io:commons-io:2.10.0',
			'junit:junit:4.13.2',
			'net.htmlparser.jericho:jericho-html:3.2',
			'com.google.code.gson:gson:2.8.7',
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
	)
	testFixturesImplementation(
			'junit:junit:4.13.2',
			testFixtures(project(':com.ibm.wala.cast')),
			testFixtures(project(':com.ibm.wala.core')),
	)
	testImplementation(
			'junit:junit:4.13.2',
			testFixtures(project(':com.ibm.wala.cast')),
			testFixtures(project(':com.ibm.wala.core')),
	)
}

tasks.register('createPackageList', CreatePackageList) {
	sourceSet sourceSets.main
}

tasks.named('javadoc') {
	classpath += files project(':com.ibm.wala.cast.js.rhino').tasks.named('compileJava', JavaCompile)
}

tasks.named('test') {
	maxHeapSize = '800M'
}

final downloadAjaxslt = tasks.register('downloadAjaxslt', VerifiedDownload) {
	def version = '0.8.1'
	def versionedArchive = "ajaxslt-${version}.tar.gz"
	src "https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/ajaxslt/$versionedArchive"
	dest project.layout.buildDirectory.file(versionedArchive)
	checksum 'c995abe3310a401bb4db7f28a6409756'
}

final unpackAjaxslt = tasks.register('unpackAjaxslt', Sync) {
	from(downloadAjaxslt.map { tarTree it.dest }) {
		eachFile {
			def newSegments = relativePath.segments[1 .. -1] as String[]
			relativePath new RelativePath(!directory, newSegments)
		}
	}
	into project.layout.buildDirectory.dir(name)
}

tasks.named('processTestResources', Copy) {
	from(unpackAjaxslt) {
		into 'ajaxslt'
	}
}

apply from: rootProject.file("gradle-mvn-push.gradle")
