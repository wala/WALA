plugins {
	id 'eclipse'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

sourceSets.test.java.srcDirs = ['src']


////////////////////////////////////////////////////////////////////////
//
//  download and extract kawa 3.0 "kawa.jar"
//

final def downloadKawa = tasks.register('downloadKawa', VerifiedDownload) {
	ext.version = '3.0'
	final def archive = "kawa-${version}.zip"
	src "https://ftp.gnu.org/pub/gnu/kawa/$archive"
	dest "$temporaryDir/$archive"
	checksum '2713e6dfb939274ba3b1d36daea68436'
}

tasks.register('extractKawa', Sync) {
	dependsOn downloadKawa
	from(zipTree(downloadKawa.get().dest)) {
		include "kawa-${downloadKawa.get().version}/lib/kawa.jar"
		eachFile {
			relativePath new RelativePath(!directory, relativePath.lastName)
		}
	}
	into temporaryDir
	includeEmptyDirs false
	outputs.file "$temporaryDir/kawa.jar"
}


////////////////////////////////////////////////////////////////////////
//
//  specialized task for Kawa compilation into jar archive
//

@CacheableTask
class CompileKawaJar extends Jar {

	@Nested final def compile = project.task("compileKawaInto${name.capitalize()}", type: JavaExec) {
		dependsOn project.extractKawa
		final def kawaJar = project.files(project.extractKawa)[0]
		classpath kawaJar
		main 'kawa.repl'

		args '-d', temporaryDir
		outputs.dir temporaryDir

		logging.captureStandardError LogLevel.INFO
		args '--main', '-C'
	}

	CompileKawaJar() {
		compile.dependsOn dependsOn
		from compile
		archiveVersion.set null
	}

	void setArgs(Object... args) {
		compile.args args
	}

	void setBaseName(String name) {
		archiveFileName.set "${name}.jar"
	}

}


////////////////////////////////////////////////////////////////////////
//
//  download, unpack, and build kawa chess
//

final def downloadKawaChess = tasks.register('downloadKawaChess', VerifiedDownload) {
	ext.commitHash = 'f1d2dcc707a1ef19dc159e2eaee5aecc8a41d7a8'
	src "https://github.com/ttu-fpclub/kawa-chess/archive/${commitHash}.zip"
	dest "$temporaryDir/kawa-chess.zip"
	checksum 'cf29613d2be5f476a475ee28b4df9d9e'
}

final def unpackKawaChess = tasks.register('unpackKawaChess', Sync) {
	dependsOn downloadKawaChess
	from zipTree(downloadKawaChess.get().dest)
	into temporaryDir
	ext.top = "$destinationDir/kawa-chess-${downloadKawaChess.get().commitHash}"
}

tasks.register('buildChessJar', CompileKawaJar) {
	dependsOn unpackKawaChess
	final def schemePath = { "${unpackKawaChess.get().top}/${it}.scm" }
	final def schemeFiles = files(['chess', 'gui', 'img', 'main', 'pos'].collect(schemePath))
	args schemePath('main')
	inputs.files schemeFiles
	baseName 'kawachess'
}


////////////////////////////////////////////////////////////////////////
//
//  build the kawa test jar
//

tasks.register('buildKawaTestJar', CompileKawaJar) {
	final def schemeFile = file('kawasrc/test.scm')
	args schemeFile
	inputs.files schemeFile
	baseName 'kawatest'
}


////////////////////////////////////////////////////////////////////////
//
//  download and extract "bcel-5.2.jar"
//

final def downloadBcel = tasks.register('downloadBcel', VerifiedDownload) {
	ext.version = '5.2'
	def archive = "bcel-${version}.tar.gz"
	src "http://archive.apache.org/dist/jakarta/bcel/binaries/$archive"
	dest "$temporaryDir/$archive"
	checksum '19bffd7f217b0eae415f1ef87af2f0bc'
	useETag false
}

tasks.register('extractBcel', Copy) {
	dependsOn downloadBcel
	from(tarTree(downloadBcel.get().dest)) {
		include "bcel-${downloadBcel.get().version}/bcel-${downloadBcel.get().version}.jar"
		eachFile {
			relativePath new RelativePath(!directory, relativePath.lastName)
		}
	}
	into projectDir
	includeEmptyDirs false
	outputs.file "bcel-${downloadBcel.get().version}.jar"
}

tasks.register('cleanExtractBcel', Delete) {
	delete files(extractBcel)[0]
}

tasks.named('clean') {
	dependsOn 'cleanExtractBcel'
}



////////////////////////////////////////////////////////////////////////
//
//  download "java-cup-11a.jar"
//

tasks.register('downloadJavaCup', VerifiedDownload) {
	def archive = 'java-cup-11a.jar'
	src "http://www2.cs.tum.edu/projects/cup/$archive"
	dest "$projectDir/$archive"
	checksum '2bda8c40abd0cbc295d3038643d6e4ec'
}

tasks.register('cleanDownloadJavaCup', Delete) {
	delete downloadJavaCup
}

tasks.named('clean') {
	dependsOn 'cleanDownloadJavaCup'
}


////////////////////////////////////////////////////////////////////////
//
//  collect "JLex.jar"
//

tasks.register('collectJLex', Jar) {
	from project(':com.ibm.wala.cast.java.test.data').compileTestJava
	include 'JLex/'
	archiveFileName.set 'JLex.jar'
	destinationDirectory.set projectDir
}

tasks.register('cleanCollectJLex', Delete) {
	delete collectJLex
}

tasks.named('clean') {
	dependsOn 'cleanCollectJLex'
}


////////////////////////////////////////////////////////////////////////
//
//  generate "hello_hash.jar"
//

final def downloadOcamlJava = tasks.register('downloadOcamlJava', VerifiedDownload) {
	def version = '2.0-alpha1'
	ext.basename = "ocamljava-$version"
	def archive = "${basename}.tar.gz"
	src "http://www.ocamljava.org/downloads/download.php?version=$version-bin"
	dest "$temporaryDir/$archive"
	checksum '45feec6e3889f5073a39c2c4c84878d1'
}

final def unpackOcamlJava = tasks.register('unpackOcamlJava', Sync) {
	dependsOn downloadOcamlJava
	from tarTree(downloadOcamlJava.get().dest)
	into temporaryDir
}

tasks.register('generateHelloHashJar', JavaExec) {
	dependsOn unpackOcamlJava
	def ocamlSource = file('ocaml/hello_hash.ml')
	inputs.file ocamlSource

	def jarTarget = file('hello_hash.jar')
	outputs.file jarTarget
	outputs.cacheIf { true }

	def ocamlJavaJar = new File("${unpackOcamlJava.get().destinationDir}/${downloadOcamlJava.get().basename}/lib/ocamljava.jar")
	inputs.file ocamlJavaJar
	classpath ocamlJavaJar

	main 'ocaml.compilers.ocamljavaMain'
	args ocamlSource, '-o', jarTarget
}

tasks.register('cleanGenerateHelloHashJar', Delete) {
	delete generateHelloHashJar
	delete fileTree('ocaml') {
		exclude '*.ml'
		exclude '.gitignore'
	}
}

tasks.named('clean') {
	dependsOn 'cleanGenerateHelloHashJar'
}


////////////////////////////////////////////////////////////////////////
//
//  collect "com.ibm.wala.core.testdata_1.0.0.jar"
//

tasks.register('collectTestData', Jar) {
	archiveFileName.set 'com.ibm.wala.core.testdata_1.0.0.jar'
	from compileTestJava
	from 'classes'
	includeEmptyDirs false
	destinationDirectory.set projectDir
}

tasks.register('cleanCollectTestData', Delete) {
	delete collectTestData
}

tasks.named('clean') {
	dependsOn 'cleanCollectTestData'
}


////////////////////////////////////////////////////////////////////////
//
//  collect "com.ibm.wala.core.testdata_1.0.0a.jar"
//

final def collectTestDataA = tasks.register('collectTestDataA', Jar) {
	archiveFileName.set 'com.ibm.wala.core.testdata_1.0.0a.jar'
	from compileTestJava
	from 'classes'
	includeEmptyDirs false
	destinationDirectory.set projectDir
	exclude (
		'**/CodeDeleted.class',
		'**/SortingExample.class',
		'**/A.class',
		)
}

tasks.register('cleanColllectTestDataA', Delete) {
	delete collectTestDataA
}

tasks.named('clean') {
	dependsOn 'cleanCollectTestDataA'
}
