plugins {
	id 'com.diffplug.eclipse.mavencentral'
}

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.contenttype',
				'org.eclipse.core.runtime',
				'org.eclipse.equinox.preferences',
				'org.eclipse.jdt.core',
				'org.eclipse.osgi',
		].each { dep 'testImplementation', it }
		useNativesForRunningPlatform()
	}
}

dependencies {
	testImplementation(
			'junit:junit:4.13.2',
			'org.eclipse.platform:org.eclipse.osgi:3.17.0',
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.java'),
			project(':com.ibm.wala.cast.java.ecj'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.ide'),
			project(':com.ibm.wala.ide.jdt'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
			project(configuration: 'testArchives', path: ':com.ibm.wala.ide.tests'),
			testFixtures(project(':com.ibm.wala.cast.java')),
	)
}

tasks.named('test') {
	maxHeapSize = '1200M'

	// https://github.com/liblit/WALA/issues/5
	exclude '**/JDTJava15IRTests.class'
	exclude '**/JDTJavaIRTests.class'

	workingDir project(':com.ibm.wala.cast.java.test.data').projectDir
}
