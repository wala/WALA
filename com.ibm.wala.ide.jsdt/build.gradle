plugins {
	id 'com.diffplug.eclipse.mavencentral'
	id 'wala-java'
}

repositories {
	maven {
		url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
		content {
			includeGroup 'org.eclipse.wst.jsdt'
		}
	}
}

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.jobs',
				'org.eclipse.core.resources',
				'org.eclipse.core.runtime',
				'org.eclipse.equinox.common',
				'org.eclipse.osgi',
				'org.eclipse.ui.workbench',
		].each it.&implementation
		useNativesForRunningPlatform()
		constrainTransitivesToThisRelease()
	}
}

dependencies {
	api(project(':com.ibm.wala.ide')) {
		because 'public class JavaScriptHeadlessUtil extends class HeadlessUtil'
	}
	implementation(
			"org.eclipse.wst.jsdt:core:$eclipseWstJsdtVersion",
			"org.eclipse.wst.jsdt:ui:$eclipseWstJsdtVersion",
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.js'),
			project(':com.ibm.wala.cast.js.rhino'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.util'),
	)
	implementation('javax.annotation:javax.annotation-api') {
		version { strictly '1.3.2' }
	}
	testFixturesImplementation('javax.annotation:javax.annotation-api') {
		version { strictly '1.3.2' }
	}
}
