plugins {
	id 'com.github.hauner.jarTest'
	id 'eclipse'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

sourceSets.test {
	java.srcDirs = ['src']
	resources.srcDirs = ['dat']
}

publishing.publications {
	mavenJava(MavenPublication) {
		artifact jarTest
	}
}

dependencies {
	testCompile(
		'junit:junit:4.12',
		'org.apache.ant:ant:1.8.2',
		'org.hamcrest:hamcrest-core:1.3',
		'org.osgi:org.osgi.core:4.2.0',
		project(':com.ibm.wala.core'),
		project(':com.ibm.wala.shrike'),
		project(':com.ibm.wala.util'),
		)
}

tasks.named('processTestResources') {
	def testdata = project(':com.ibm.wala.core.testdata')
	dependsOn testdata.tasks.named('compileTestJava')
	dependsOn testdata.tasks.named('extractBcel')

	from testdata.collectJLex
	from testdata.collectTestData
	from testdata.downloadJavaCup
	from files(testdata.extractBcel)[0]
	from testdata.generateHelloHashJar
	from testdata.extractKawa
	from testdata.buildChessJar
	from testdata.buildKawaTestJar
}

tasks.named('test') {
	maxHeapSize = '1500M'
	systemProperty 'com.ibm.wala.junit.analyzingJar', 'true'
	systemProperty 'com.ibm.wala.junit.profile', 'short'
	classpath += files project(':com.ibm.wala.core.testdata').sourceSets.test.java.outputDir
        testLogging {
            exceptionFormat = 'full'
        }
        // temporarily turn off some tests on JDK 11
        if (JavaVersion.current() == JavaVersion.VERSION_11) {
           exclude '**/callGraph/CallGraphTest.class'
           exclude '**/callGraph/Java7CallGraphTest.class'
           exclude '**/callGraph/CloneTest.class'
           exclude '**/callGraph/DebuggingBitsetCallGraphTest.class'
           exclude '**/callGraph/KawaCallGraphTest.class'
           exclude '**/exceptionpruning/ExceptionAnalysisTest.class'
           exclude '**/exceptionpruning/ExceptionAnalysis2EdgeFilterTest.class'
           exclude '**/cha/LibraryVersionTest.class'
           exclude '**/ir/TypeAnnotationTest.class'
        }
}

tasks.register('cleanTestExtras', Delete) {
	delete 'report'
}

tasks.named('cleanTest') {
	dependsOn 'cleanTestExtras'
}
