plugins {
	id 'com.diffplug.gradle.eclipse.mavencentral'
}

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.runtime',
				'org.eclipse.jdt.core',
		].each { dep 'testImplementation', it }
	}
}

dependencies {
	testImplementation(
			'junit:junit:4.13',
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.java'),
			project(':com.ibm.wala.cast.java.ecj'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
			testFixtures(project(':com.ibm.wala.cast.java.test')),
	)
}

tasks.named('test') {
	maxHeapSize = '1200M'
	
	workingDir project(':com.ibm.wala.cast.java.test.data').projectDir

	// temporarily turn off some tests on JDK 9+
	if (JavaVersion.current() <= JavaVersion.VERSION_1_8) {
		// ensure the command-line driver for running ECJ works
		dependsOn ':com.ibm.wala.cast.java.ecj:run'
	} else {
		exclude '**/ECJJava17IRTest.class'
		exclude '**/ECJJavaIRTest.class'
		exclude '**/ECJSyncDuplicatorTest.class'
	}
}
