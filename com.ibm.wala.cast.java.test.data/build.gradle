plugins {
	id 'wala-java'
}

tasks.named("compileTestJava").configure {
	// No need to run Error Prone on our analysis test inputs
	options.errorprone.enabled = false
	options.compilerArgs -= '-Werror'
}

////////////////////////////////////////////////////////////////////////
//
//  download JLex
//

final downloadJLex = tasks.register('downloadJLex', VerifiedDownload) {
	src 'https://www.cs.princeton.edu/~appel/modern/java/JLex/current/Main.java'
	checksum 'fe0cff5db3e2f0f5d67a153cf6c783af'
	ext.downloadedSourceDir = layout.buildDirectory.dir name
	dest downloadedSourceDir.map { "$it/JLex/Main.java" }
}

sourceSets.test.java.srcDir downloadJLex.map { it.downloadedSourceDir }


////////////////////////////////////////////////////////////////////////
//
//  create Eclipse metadata for use by Maven when running
//  com.ibm.wala.cast.java.test.JDTJavaIRTests and
//  com.ibm.wala.cast.java.test.JDTJava15IRTests tests
//

tasks.register('prepareMavenBuild') {
	dependsOn 'eclipseClasspath', 'eclipseProject'
}
