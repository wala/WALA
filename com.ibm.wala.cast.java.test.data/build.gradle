plugins {
	id 'eclipse'
}

sourceSets.test.java.srcDirs = ['src']


////////////////////////////////////////////////////////////////////////
//
//  download JLex
//

final def downloadJLex = tasks.register('downloadJLex', VerifiedDownload) {
	src 'http://www.cs.princeton.edu/~appel/modern/java/JLex/current/Main.java'
	checksum 'fe0cff5db3e2f0f5d67a153cf6c783af'
	dest 'src/JLex/Main.java'
}

tasks.register('cleanDownloadJLex', Delete) {
	delete downloadJLex.get().dest.parent
}

tasks.named('compileTestJava') {
	dependsOn 'downloadJLex'
}

tasks.named('clean') {
	dependsOn 'cleanDownloadJLex'
}


////////////////////////////////////////////////////////////////////////
//
//  create Eclipse metadata for use by Maven when running
//  com.ibm.wala.cast.java.test.JDTJavaIRTests and
//  com.ibm.wala.cast.java.test.JDTJava15IRTests tests
//

tasks.register('prepareMavenBuild') {
	dependsOn 'eclipseClasspath', 'eclipseProject'
}
