plugins {
	id 'com.github.hauner.jarTest'
}

sourceSets.test.java.srcDirs = ['harness-src']

dependencies {
	testArchives(
			'junit:junit:4.12',
			project(':com.ibm.wala.cast.js.rhino'),
	)
	testImplementation(
			'commons-io:commons-io:2.6',
			'junit:junit:4.12',
			'net.htmlparser.jericho:jericho-html:3.2',
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.js'),
			project(':com.ibm.wala.cast.js.rhino'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.shrike'),
			project(':com.ibm.wala.util'),
			project(configuration: 'testArchives', path: ':com.ibm.wala.cast.js.test'),
			project(configuration: 'testArchives', path: ':com.ibm.wala.cast.test'),
			project(configuration: 'testArchives', path: ':com.ibm.wala.core.tests'),
	)
}

tasks.named('processTestResources') {
	def data = project(':com.ibm.wala.cast.js.test.data')
	dependsOn data.tasks.named('processTestResources')
	from data.sourceSets.test.resources
}

tasks.named('test') {
	environment 'TRAVIS', 1
	maxHeapSize = '800M'

	if (gradle.startParameter.offline)
		exclude '**/FieldBasedJQueryTest.class'
}

tasks.register('cleanTestExtras', Delete) {
	delete 'actual.dump'
	delete 'expected.dump'
}

tasks.named('cleanTest') {
	dependsOn 'cleanTestExtras'
}
