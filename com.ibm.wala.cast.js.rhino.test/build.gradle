plugins {
	id 'com.github.hauner.jarTest'
}

sourceSets.test.java.srcDirs = ['harness-src']

dependencies {
	testCompile(
		'junit:junit:4.12',
		project(':com.ibm.wala.cast'),
		project(':com.ibm.wala.cast.js'),
		project(':com.ibm.wala.cast.js.rhino'),
		project(':com.ibm.wala.core'),
		project(':com.ibm.wala.shrike'),
		project(':com.ibm.wala.util'),
		project(configuration: 'testArchives', path: ':com.ibm.wala.cast.js.test'),
		project(configuration: 'testArchives', path: ':com.ibm.wala.cast.test'),
		project(configuration: 'testArchives', path: ':com.ibm.wala.core.tests'),
		)
}

processTestResources {
	def data = project(':com.ibm.wala.cast.js.test.data')
	dependsOn data.processTestResources
	from data.sourceSets.test.resources
}

test {
	environment 'TRAVIS', 1
	maxHeapSize = '800M'

	if (gradle.startParameter.offline)
		exclude '**/FieldBasedJQueryTest.class'
}

task cleanTestExtras(type: Delete) {
	delete 'actual.dump'
	delete 'expected.dump'
}

cleanTest.dependsOn cleanTestExtras
