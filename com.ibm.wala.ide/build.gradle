plugins {
	id 'com.diffplug.eclipse.mavencentral'
	id 'eclipse'
}

eclipse.project.natures 'org.eclipse.pde.PluginNature'

eclipseMavenCentral {
	release eclipseVersion, {
		api 'org.eclipse.pde.core'
		[
				'org.eclipse.core.commands',
				'org.eclipse.core.jobs',
				'org.eclipse.core.resources',
				'org.eclipse.core.runtime',
				'org.eclipse.equinox.common',
				'org.eclipse.jdt.core',
				'org.eclipse.jface',
				'org.eclipse.osgi',
				'org.eclipse.swt',
				'org.eclipse.ui.workbench',
		].each it.&implementation
		useNativesForRunningPlatform()
	}
}

dependencies {
	implementation(
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.util'),
	)
	implementation('javax.annotation:javax.annotation-api') {
		version { strictly '1.3.2' }
	}
	implementation('org.eclipse.platform:org.eclipse.core.resources') {
		version { strictly '3.14.0' }
	}
	implementation('org.eclipse.platform:org.eclipse.equinox.common') {
		version { strictly '3.14.100' }
	}	
	testFixturesImplementation('javax.annotation:javax.annotation-api') {
		version { strictly '1.3.2' }
	}
}
